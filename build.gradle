/*
 Copyright (c) 2010 - 2021 Marvin Horter.
 All rights reserved. This program and the accompanying materials
 are made available under the terms of the GNU Public License v2.0
 which accompanies this distribution, and is available at
 http://www.gnu.org/licenses/old-licenses/gpl-2.0.html
 */

buildscript {
    ext {
        sdkVersion = 33
        buildToolsVersion = '33.0.0'
        aboutLibrariesVersion = '10.4.1-a01'
    }
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.3.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.10"
        classpath "com.mikepenz.aboutlibraries.plugin:aboutlibraries-plugin:$aboutLibrariesVersion"
    }
}

plugins {
    id "net.ltgt.errorprone" version "1.2.1"
    id "scabbard.gradle" version "0.5.0"
//    id "com.mikepenz.aboutlibraries.plugin" version "aboutLibrariesVersion"
}

scabbard {
    enabled true
    outputFormat "svg"
    fullBindingGraphValidation true
}

dependencies {
    errorprone "com.google.errorprone:error_prone_core:2.4.0"
}

afterEvaluate {
    tasks.withType(JavaCompile).configureEach {
        options.errorprone.disableWarningsInGeneratedCode = true
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked"
        }
    }

    configurations.all {
        resolutionStrategy.eachDependency {
            if (requested.group == "com.github.kittinunf.result" && requested.name == "result" && requested.version == "3.0.0") {
                useVersion("3.0.1")
                because("Transitive dependency of Scabbard, currently not available on mavenCentral()")
            }
        }
    }
}
