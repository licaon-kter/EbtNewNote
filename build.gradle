/*
 Copyright (c) 2010 - 2022 Marvin Horter.
 All rights reserved. This program and the accompanying materials
 are made available under the terms of the GNU Public License v2.0
 which accompanies this distribution, and is available at
 http://www.gnu.org/licenses/old-licenses/gpl-2.0.html
 */

buildscript {
    ext {
        buildToolsVersion = "${libs.versions.compileSdk.get().toInteger()}.0.0"
    }
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath libs.androidGradlePlugin
        classpath libs.kotlin.gradle.plugin
        classpath libs.dependency.check
    }
}

plugins {
    alias libs.plugins.aboutlibraries apply false
    alias libs.plugins.errorprone
    alias libs.plugins.cyclonedx
    alias libs.plugins.scabbard
}

scabbard {
    enabled true
    outputFormat "svg"
    fullBindingGraphValidation true
}

dependencies {
    errorprone libs.error.prone.core
}

afterEvaluate {
    tasks.withType(JavaCompile).configureEach {
        options.errorprone.disableWarningsInGeneratedCode = true
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        maven { url 'https://jitpack.io' }
    }

    configurations.configureEach {
        resolutionStrategy.eachDependency {
            if (requested.group == "com.github.kittinunf.result" && requested.name == "result" && requested.version == "3.0.0") {
                useVersion("3.0.1")
                because("Transitive dependency of Scabbard, currently not available on mavenCentral()")
            }
        }
    }
}
